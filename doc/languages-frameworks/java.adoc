[[_sec_language_java]]
= Java


Ant-based Java packages are typically built from source as follows: 
[source]
----

stdenv.mkDerivation {
  name = "...";
  src = fetchurl { ... };

  nativeBuildInputs = [ jdk ant ];

  buildPhase = "ant";
}
----

Note that [var]``jdk`` is an alias for the OpenJDK (self-built where available, or pre-built via Zulu). Platforms with OpenJDK not (yet) in Nixpkgs (``Aarch32``, ``Aarch64``) point to the (unfree) ``oraclejdk``. 

JAR files that are intended to be used by other packages should be installed in [path]``$out/share/java``
.
JDKs have a stdenv setup hook that add any JARs in the [path]``share/java``
 directories of the build inputs to the [var]``CLASSPATH`` environment variable.
For instance, if the package `libfoo` installs a JAR named [path]``foo.jar``
 in its [path]``share/java``
 directory, and another package declares the attribute 
[source]
----

buildInputs = [ libfoo ];
nativeBuildInputs = [ jdk ];
----

then [var]``CLASSPATH`` will be set to [path]``/nix/store/...-libfoo/share/java/foo.jar``
. 

Private JARs should be installed in a location like [path]``$out/share/package-name``
. 

If your Java package provides a program, you need to generate a wrapper script to run it using the OpenJRE.
You can use `makeWrapper` for this: 
[source]
----

nativeBuildInputs = [ makeWrapper ];

installPhase =
  ''
    mkdir -p $out/bin
    makeWrapper ${jre}/bin/java $out/bin/foo \
      --add-flags "-cp $out/share/java/foo.jar org.foo.Main"
  '';
----

Note the use of ``jre``, which is the part of the OpenJDK package that contains the Java Runtime Environment.
By using `${jre}/bin/java` instead of ``${jdk}/bin/java``, you prevent your package from depending on the JDK at runtime. 

Note all JDKs passthru ``home``, so if your application requires environment variables like [var]``JAVA_HOME`` being set, that can be done in a generic fashion with the `--set` argument of ``makeWrapper``: 
[source]
----

--set JAVA_HOME ${jdk.home}
----

It is possible to use a different Java compiler than [command]``javac`` from the OpenJDK.
For instance, to use the GNU Java Compiler: 
[source]
----

nativeBuildInputs = [ gcj ant ];
----

Here, Ant will automatically use [command]``gij`` (the GNU Java Runtime) instead of the OpenJRE. 